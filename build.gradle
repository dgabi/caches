plugins {
    id "java"
    id "checkstyle"
    id "com.github.spotbugs" version "4.5.1"
    id "com.simonscholz.reports" version "0.0.1"
}

group "sandbox"
version "1.0-SNAPSHOT"

repositories {
    mavenCentral()
}

sourceSets {
    integration {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration/java')
        }
    }
}

dependencies {
    implementation  "org.apache.logging.log4j:log4j-core:2.13.3"

    testImplementation "org.assertj:assertj-core:3.11.1"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
    testImplementation "org.mockito:mockito-core:2.+"

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    integrationRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

}

configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
}

tasks.withType(Test) {
    useJUnitPlatform()
}

test {
    testLogging {
        outputs.upToDateWhen {false}
        showStandardStreams = true
    }
}


task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
}

check.dependsOn integrationTest


checkstyle {
    toolVersion '8.36'
    configFile file("${rootDir}/config/checkstyle/checkstyle.xml")
}
checkstyleMain {
    source ='src/main/java'
}
checkstyleTest {
    source ='src/test/java'
}
